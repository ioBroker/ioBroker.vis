!# devices.fn
!#
!# Dieses Homematic-Script gibt die Geräteliste mitsamt Kanälen
!# und Datenpunkten als JSON Objekt aus
!#
!# 3'2013 hobbyquaker https://github.com/hobbyquaker
!#

string sDevId;
string sChnId;
string sDPId;

Write('{');

    boolean dFirst = true;

    foreach (sDevId, root.Devices().EnumUsedIDs()) {
    if (dFirst) {
      dFirst = false;
    } else {
      WriteLine(',');
    }

    object oDevice   = dom.GetObject(sDevId);
    boolean bDevReady = oDevice.ReadyConfig();
    string sDevInterfaceId = oDevice.Interface();
    string sDevInterface   = dom.GetObject(sDevInterfaceId).Name();

    if (bDevReady) {

        Write('"_' # sDevId # '":{"Name":"');
        WriteURL(oDevice.Name());
        Write('","Address":"' # oDevice.Address() # '","Interface":"' # sDevInterface # '","HssType":"' # oDevice.HssType());
        Write('","Channels":{');

        string sChnId;
        string sDPId;
        boolean cFirst = true;

        foreach(sChnId, oDevice.Channels()) {
            object oChannel = dom.GetObject(sChnId);
            if ( (! oChannel.Internal()) ) {
                if (cFirst) {
                  cFirst = false;
                } else {
                  Write(',');
                }

                integer iChnDir     = oChannel.ChnDirection();
                Write('\n"_' # sChnId # '":{"Name":"');
                WriteURL(oChannel.Name());
                Write('","HssType":"' # oChannel.HssType());
                Write('","ChnDirection":"' # iChnDir # '","ChannelType":"');
                Write(oChannel.ChannelType());
                Write('","Address":"' # oChannel.Address());
                Write('","ChnLabel":"' # oChannel.ChnLabel());
                WriteLine('","DPs":{');

                boolean dpFirst = true;

                if (iChnDir == 2) {
                    object oWork = oChannel.DPByHssDP('WORKING');
                    if (oWork) {
                        dpFirst = false;
                        string oWId = oWork.ID();
                        string oWName = oWork.Name();
                        Write('"_' # oWId # '":{');
                        Write('"Name":"' # oWork.Name() # '","Type":"WORKING","dptype":"HSSDP","oper":"5"}');
                    }
                }

                foreach(sDPId, oChannel.DPs().EnumUsedIDs()) {
                    object oDP = dom.GetObject(sDPId);
                    if(oDP) {
                        if (dpFirst) {
                          dpFirst = false;
                        } else {
                          WriteLine(',');
                        }

                        string dp = oDP.Name().StrValueByIndex('.', 2);
                        Write('"_' # sDPId # '":{');
                        Write('"Name":"'); WriteXML(oDP.Name());
                        Write('","Type":"'); WriteXML(oDP.Name().StrValueByIndex('.', 2));
                        Write('","TypeName":"'); WriteXML(oDP.TypeName());
                        Write('","Operations":"'); WriteXML(oDP.Operations());
                        Write('","ValueType":"' # oDP.ValueType());
                        Write('","ValueUnit":"' # oDP.ValueUnit());
                        Write('"}');
                    }
                }
                Write('}}');
            }
        }
        Write('}}');
    }
}
Write('}');
